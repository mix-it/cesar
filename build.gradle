/*******************************************************************************************
 *                              gradle file : ROOT
 ******************************************************************************************/

buildscript {
    ext {
        springRepo = 'http://repo.spring.io/libs-release-remote'

        vAssertJ = '3.1.0'
        vDbsetup = '1.6.0'
        vGuava = '18.0'
        vH2 = '1.3.170'
        vJava = '1.8'
        vJunit = '4.12'
        vJUnitParams = '1.0.4'
        vFlyway = '3.2.1'
        vMockito = '1.10.19'
        vNodePlugin = '0.10'
        vMysql = '5.1.36'
        vSl4j = '1.7.12'
        vSpringBoot = '1.3.1.RELEASE'
        vSwagger = '2.0.3'
    }

    repositories {
        maven { url springRepo }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.moowork.gradle:gradle-gulp-plugin:${vNodePlugin}"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${vSpringBoot}"
        classpath "org.flywaydb:flyway-gradle-plugin:${vFlyway}"
        classpath 'net.researchgate:gradle-release:2.3.3'
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.7"

        classpath "com.h2database:h2:${vH2}"
        classpath "mysql:mysql-connector-java:${vMysql}"
    }
}

apply plugin: 'java'
apply plugin: 'net.researchgate.release'
apply plugin: 'com.gorylenko.gradle-git-properties'

group 'org.mixit'

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

/**
 * Manage git version
 */
gitProperties {
    gitPropertiesDir = new File("${project.rootDir}")
}

/*******************************************************************************************
 * The different plugins used in submodules and dependencies are defined here
 ******************************************************************************************/

//Default IDE is IntelliJ and we use the jar packaging
configure(allprojects) {
    apply plugin: 'idea'
    apply plugin: 'java'

    targetCompatibility = vJava
    sourceCompatibility = vJava
}

//We use gulp to manage the webapp
configure([project(':client-ng1')]) {
    apply plugin: 'com.moowork.node'
}

//Flyway is used to manage the database
configure([project(':common')]) {
    apply plugin: 'org.flywaydb.flyway'

    repositories {
        maven { url springRepo }
    }

    dependencies {
        compile "mysql:mysql-connector-java:${vMysql}"
    }
}

//Uses Spring Boot
configure([project(':server')]) {
    apply plugin: 'spring-boot'

    repositories {
        maven { url springRepo }
    }

    dependencies {

        compile "org.springframework.boot:spring-boot-starter-actuator"
        compile "org.springframework.boot:spring-boot-starter-web"
        compile "org.springframework.boot:spring-boot-starter-hateoas"
        compile "org.springframework.boot:spring-boot-starter-data-jpa"
        compile "org.springframework.boot:spring-boot-starter-cache"
        compile "org.springframework.boot:spring-boot-starter-velocity"
        compile "org.springframework.boot:spring-boot-starter-websocket"
        compile("org.springframework.boot:spring-boot-devtools")

        compile "com.google.zxing:javase:3.2.1"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.1"
        compile "com.google.guava:guava:${vGuava}"
        compile "org.slf4j:slf4j-api:${vSl4j}"
        compile "com.h2database:h2:${vH2}"
        compile "mysql:mysql-connector-java:${vMysql}"
        compile "io.springfox:springfox-swagger2:${vSwagger}"
        compile "io.springfox:springfox-swagger-ui:${vSwagger}"
        compile "org.jadira.usertype:usertype.extended:3.2.0.GA"
        compile "org.scribe:scribe:1.3.5"
        compile "javax.mail:mail:1.4.7"

        testCompile "junit:junit:${vJunit}"
        testCompile "pl.pragmatists:JUnitParams:${vJUnitParams}"
        testCompile "org.assertj:assertj-core:${vAssertJ}"
        testCompile "org.mockito:mockito-core:${vMockito}"
        testCompile "org.springframework:spring-test"
        testCompile "com.ninja-squad:DbSetup:${vDbsetup}"
        testCompile "com.jayway.jsonpath:json-path-assert:0.8.1"
    }

    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        all*.exclude group: 'log4j', module: 'log4j'
    }

}
